/**

	----------------- ISWITCH MINDFUCK ( HOW MUCH CODE IS NEEDED TO MAKE ONE FUCKING BUTTON WORKING ) -----------------


	1. Script runs FIRST time on Zbrush startup, memory blocks are created in this time


	2. Script runs EVERY time any control is fired, if scripth has been interrupted by another plugin
		( If another plugin or script is used then first fire of control RUNS WHOLE SCRIPT AGAIN )


	3. First click on iSwitch FIRES EVENT OF INIT STATE, NOT OPPOSITE STATE as should be excepted
  		( if PRESSED, then FIRES PRESSED, if UNPRESSED, then fires UNPRESSED )

		BUT THIS HAPPEN ONLY IF ISWITCH IS IN INIT STATE
			( if INIT STATE is PRESSED and iSwitch is UNPRESSED, then works normaly  )


*/

/* INIT STATE OF SWITCH */
[VarSet, $INIT_SWITCH_STATE, 0 ]
//[VarSet, $INIT_SWITCH_STATE, 1 ]

[VarDef, $INIT_STATE(1), 0]

[VarDef, $CONFIGFILE,	[FileNameResolvePath, "TemplateData\iSwitch\switchConfig.zvr"] ]

[VarDef, $plugin_initialized, [MemGetSize, CURRENT_STATE ] > 0 ] // this avoid run code on startp of zbrush


/** dataFileSave
  *
  */
[RoutineDef, dataFileSave
,
	[VarSet, $INIT_STATE([Var, $index]), $value_to_save ]

	[VarSave, $INIT_STATE, $CONFIGFILE ]

	//[Note, [StrMerge, "dataFile.configSave \n\nIndex: ", $index, "\n\nValue: ",  $value_to_save ],, 0]

, // PARAMS
	$index, $value_to_save
]


[If, ! [MemGetSize, CURRENT_STATE ]
,
	/* DEFINE MEMORY BLOCK */
	[MVarDef, CURRENT_STATE,	1 ]

	/* SET DEFAULTS */
	[MVarSet, CURRENT_STATE,	0, [Var, $INIT_SWITCH_STATE] ]

	[If, ! [FileExists, $CONFIGFILE]
	, // THEN

		[RoutineCall, dataFileSave, 0, [Var, $INIT_SWITCH_STATE] ]
	]

	[VarLoad, $INIT_STATE, $CONFIGFILE ]

	[MVarSet, CURRENT_STATE,	0, [Var, $INIT_STATE(0)] ]

]



/*  UI */
[VarSet, $PLUGIN_NENU, "~CONFIG_TEST"]

[IPalette, $PLUGIN_NENU ]

[VarSet, $switch_button,	[StrMerge, $PLUGIN_NENU, ":", "Switch"] ]


/* ----- SWITCH  BUTTON----- */
[ISwitch, $switch_button, [Var, $INIT_STATE(0)], ""
, // PRESS

	[Note, "Enable",, 0.5]

	[MVarSet, CURRENT_STATE, 0, 1 ]

	[RoutineCall, dataFileSave, 0, 1 ]

, // UNPRESS

	[Note, "Disable",, 0.5]

	[MVarSet, CURRENT_STATE, 0, 0 ]

	[RoutineCall, dataFileSave, 0, 0 ]

,,,	48]


/* FORCE SWITCH TO CORRECT STATE ON FIRST HIT, BUT NOT ON ZBRUSH STARTUP */
[If, [Var, $plugin_initialized] && ([MVarGet, CURRENT_STATE, 0 ] == [Var, $INIT_STATE(0)] )
, // THEN
	[ISet,  $switch_button, ! [MVarGet, CURRENT_STATE, 0 ]  ]
]







